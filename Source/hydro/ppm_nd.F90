module ppm_module


  ! this does the parabolic reconstruction on a variable and the (optional)
  ! integration under the characteristic domain of the parabola

  use amrex_constants_module, only: ZERO, HALF, ONE, TWO, SIXTH, &
                                    TWO3RD, THREE, SIX, SEVEN12TH, TWELFTH
  use prob_params_module, only : dg
  use amrex_fort_module, only : rt => amrex_real

  implicit none

  private

  public ppm_reconstruct, ppm_int_profile

contains

  subroutine ppm_reconstruct(s, s_lo, s_hi, ncomp, n, &
                             flatn, f_lo, f_hi, &
                             sxm, sxp, &
#if BL_SPACEDIM >= 2
                             sym, syp, &
#endif
#if BL_SPACEDIM == 3
                             szm, szp, &
#endif
                             sd_lo, sd_hi, &
                             ilo1, ilo2, ihi1, ihi2, dx, k3d, kc, &
                             force_type_in)

    ! perform the ppm reconstruction on component n in the array s and
    ! store the limits of the parabola in s[xyz][mp] indexed with ic.

    ! Here, s[xyz][mp] is for only a single component
    ! s has ncomp components

    use meth_params_module, only : ppm_type

    implicit none

    integer, intent(in) ::  s_lo(3),  s_hi(3)
    integer, intent(in) :: ncomp, n
    integer, intent(in) ::  sd_lo(3),  sd_hi(3)
    integer, intent(in) ::  f_lo(3),  f_hi(3)
    integer, intent(in) :: ilo1, ilo2, ihi1, ihi2
    integer, intent(in) :: k3d, kc

    real(rt)        , intent(in) ::     s( s_lo(1): s_hi(1), s_lo(2): s_hi(2), s_lo(3): s_hi(3), ncomp)
    real(rt)        , intent(in) :: flatn( f_lo(1): f_hi(1), f_lo(2): f_hi(2), f_lo(3): f_hi(3))
    real(rt)        , intent(inout) :: sxm( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt)        , intent(inout) :: sxp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#if BL_SPACEDIM >= 2
    real(rt)        , intent(inout) :: sym( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt)        , intent(inout) :: syp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#endif
#if BL_SPACEDIM == 3
    real(rt)        , intent(inout) :: szm( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt)        , intent(inout) :: szp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#endif
    real(rt)        , intent(in) :: dx(3)

    integer, intent(in), optional :: force_type_in

    integer :: ppm_type_to_use

    ppm_type_to_use = ppm_type
    if (present(force_type_in)) ppm_type_to_use = force_type_in

    if (ppm_type_to_use == 1) then

        call ppm_type1(s, s_lo, s_hi, ncomp, n, &
                       flatn, f_lo, f_hi, &
                       sxm, sxp, &
#if BL_SPACEDIM >= 2
                       sym, syp, &
#endif
#if BL_SPACEDIM == 3
                       szm, szp, &
#endif
                       sd_lo, sd_hi, &
                       ilo1, ilo2, ihi1, ihi2, dx, k3d, kc)

    else if (ppm_type_to_use == 2) then

        call ppm_type2(s, s_lo, s_hi, ncomp, n, &
                       flatn, f_lo, f_hi, &
                       sxm, sxp, &
#if BL_SPACEDIM >= 2
                       sym, syp, &
#endif
#if BL_SPACEDIM == 3
                       szm, szp, &
#endif
                       sd_lo, sd_hi, &
                       ilo1, ilo2, ihi1, ihi2, dx, k3d, kc)

    end if

  end subroutine ppm_reconstruct

  ! :::
  ! ::: ----------------------------------------------------------------
  ! :::

  subroutine ppm_type1(s, s_lo, s_hi, ncomp, n, &
                       flatn, f_lo, f_hi, &
                       sxm, sxp, &
#if BL_SPACEDIM >= 2
                       sym, syp, &
#endif
#if BL_SPACEDIM == 3
                       szm, szp, &
#endif
                       sd_lo, sd_hi, &
                       ilo1, ilo2, ihi1, ihi2, dx, k3d, kc)

    use amrex_mempool_module, only : bl_allocate, bl_deallocate
    use meth_params_module, only : ppm_type

    use amrex_error_module
    use amrex_fort_module, only : rt => amrex_real
    implicit none

    integer, intent(in) ::  s_lo(3),  s_hi(3)
    integer, intent(in) :: ncomp, n
    integer, intent(in) :: sd_lo(3), sd_hi(3)
    integer, intent(in) ::  f_lo(3),  f_hi(3)
    integer, intent(in) :: ilo1, ilo2, ihi1, ihi2
    integer, intent(in) :: k3d, kc

    real(rt), intent(in) ::     s( s_lo(1): s_hi(1), s_lo(2): s_hi(2), s_lo(3): s_hi(3), ncomp)
    real(rt), intent(in) :: flatn( f_lo(1): f_hi(1), f_lo(2): f_hi(2), f_lo(3): f_hi(3))
    real(rt), intent(inout) :: sxm( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(inout) :: sxp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#if BL_SPACEDIM >= 2
    real(rt), intent(inout) :: sym( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(inout) :: syp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#endif
#if BL_SPACEDIM == 3
    real(rt), intent(inout) :: szm( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(inout) :: szp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#endif
    real(rt), intent(in) :: dx(3)

    ! local
    integer i,j,k

    real(rt)         dsl, dsr, dsc

    ! s_{\ib,+}, s_{\ib,-}
    real(rt)         :: sm, sp

    ! \delta s_{\ib}^{vL}
    real(rt)        , pointer :: dsvl(:,:)
    real(rt)         :: dsvlm, dsvl0, dsvlp

    ! s_{i+\half}^{H.O.}
    real(rt)        , pointer :: sedge(:,:)

#ifndef AMREX_USE_CUDA    
    if (ppm_type .ne. 1) &
         call amrex_error("Should have ppm_type = 1 in ppm_type1")

    if (s_lo(1) .gt. ilo1-3 .or. s_hi(1) .lt. ihi1+3) then 
         call amrex_error("Need more ghost cells on array in ppm_type1")
    end if

#if (BL_SPACEDIM >= 2)
    if (s_lo(2) .gt. ilo2-3 .or. s_hi(2) .lt. ihi2+3) then
         call amrex_error("Need more ghost cells on array in ppm_type1")
    end if
#endif
#endif
    ! cell-centered indexing w/extra ghost cell
    call bl_allocate(dsvl, ilo1-2, ihi1+2, ilo2-2*dg(2), ihi2+2*dg(2))

    ! edge-centered indexing
    call bl_allocate(sedge, ilo1-1, ihi1+2, ilo2-dg(2), ihi2+2*dg(2))

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! x-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    ! compute s at x-edges

    ! compute van Leer slopes in x-direction
    do j=ilo2-dg(2),ihi2+dg(2)
       do i=ilo1-2,ihi1+2
          dsc = HALF * (s(i+1,j,k3d,n) - s(i-1,j,k3d,n))
          dsl = TWO  * (s(i  ,j,k3d,n) - s(i-1,j,k3d,n))
          dsr = TWO  * (s(i+1,j,k3d,n) - s(i  ,j,k3d,n))
          if (dsl*dsr .gt. ZERO) then
             dsvl(i,j) = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
          else
             dsvl(i,j) = ZERO
          end if
       end do
    end do

    ! interpolate s to x-edges
    do j=ilo2-dg(2),ihi2+dg(2)
       !dir$ ivdep
       do i=ilo1-1,ihi1+2
          sedge(i,j) = HALF*(s(i,j,k3d,n)+s(i-1,j,k3d,n)) &
               - SIXTH*(dsvl(i,j)-dsvl(i-1,j))
          ! make sure sedge lies in between adjacent cell-centered values
          sedge(i,j) = max(sedge(i,j),min(s(i,j,k3d,n),s(i-1,j,k3d,n)))
          sedge(i,j) = min(sedge(i,j),max(s(i,j,k3d,n),s(i-1,j,k3d,n)))
       end do
    end do

    do j=ilo2-dg(2),ihi2+dg(2)
       do i=ilo1-1,ihi1+1

          ! copy sedge into sp and sm
          sm = sedge(i  ,j)
          sp = sedge(i+1,j)

          ! flatten the parabola BEFORE doing the other
          ! monotonization -- this is the method that Flash does
          sm = flatn(i,j,k3d)*sm + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)
          sp = flatn(i,j,k3d)*sp + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)

          ! modify using quadratic limiters -- note this version of the limiting comes
          ! from Colella and Sekora (2008), not the original PPM paper.
          if ((sp-s(i,j,k3d,n))*(s(i,j,k3d,n)-sm) .le. ZERO) then
             sp = s(i,j,k3d,n)
             sm = s(i,j,k3d,n)

          else if (abs(sp-s(i,j,k3d,n)) .ge. TWO*abs(sm-s(i,j,k3d,n))) then
          !else if (-(sp-sm)**2/SIX > &
          !     (sp - sm)*(s(i,j,k3d) - HALF*(sm + sp))) then
             sp = THREE*s(i,j,k3d,n) - TWO*sm

          else if (abs(sm-s(i,j,k3d,n)) .ge. TWO*abs(sp-s(i,j,k3d,n))) then
          !else if ((sp-sm)*(s(i,j,k3d) - HALF*(sm + sp)) > &
          !     (sp - sm)**2/SIX) then
             sm = THREE*s(i,j,k3d,n) - TWO*sp
          end if

          sxp(i,j,kc) = sp
          sxm(i,j,kc) = sm

       end do
    end do

#if (BL_SPACEDIM >= 2)
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! y-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    ! compute s at y-edges

    ! compute van Leer slopes in y-direction
    do j=ilo2-2,ihi2+2
       do i=ilo1-1,ihi1+1
          dsc = HALF * (s(i,j+1,k3d,n) - s(i,j-1,k3d,n))
          dsl = TWO  * (s(i,j  ,k3d,n) - s(i,j-1,k3d,n))
          dsr = TWO  * (s(i,j+1,k3d,n) - s(i,j  ,k3d,n))
          if (dsl*dsr .gt. ZERO) then
             dsvl(i,j) = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
          else
             dsvl(i,j) = ZERO
          end if
       end do
    end do

    ! interpolate s to y-edges
    do j=ilo2-1,ihi2+2
       !dir$ ivdep
       do i=ilo1-1,ihi1+1
          sedge(i,j) = HALF*(s(i,j,k3d,n)+s(i,j-1,k3d,n)) &
               - SIXTH*(dsvl(i,j)-dsvl(i,j-1))
          ! make sure sedge lies in between adjacent cell-centered values
          sedge(i,j) = max(sedge(i,j),min(s(i,j,k3d,n),s(i,j-1,k3d,n)))
          sedge(i,j) = min(sedge(i,j),max(s(i,j,k3d,n),s(i,j-1,k3d,n)))
       end do
    end do

    do j=ilo2-1,ihi2+1
       do i=ilo1-1,ihi1+1

          ! copy sedge into sp and sm
          sm = sedge(i,j  )
          sp = sedge(i,j+1)

          ! flatten the parabola BEFORE doing the other
          ! monotonization -- this is the method that Flash does
          sm = flatn(i,j,k3d)*sm + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)
          sp = flatn(i,j,k3d)*sp + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)

          ! modify using quadratic limiters
          if ((sp-s(i,j,k3d,n))*(s(i,j,k3d,n)-sm) .le. ZERO) then
             sp = s(i,j,k3d,n)
             sm = s(i,j,k3d,n)

          else if (abs(sp-s(i,j,k3d,n)) .ge. TWO*abs(sm-s(i,j,k3d,n))) then
          !else if (-(sp-sm)**2/SIX > &
          !     (sp - sm)*(s(i,j,k3d) - HALF*(sm + sp))) then
             sp = THREE*s(i,j,k3d,n) - TWO*sm

          else if (abs(sm-s(i,j,k3d,n)) .ge. TWO*abs(sp-s(i,j,k3d,n))) then
          !else if ((sp-sm)*(s(i,j,k3d) - HALF*(sm + sp)) > &
          !     (sp - sm)**2/SIX) then
             sm = THREE*s(i,j,k3d,n) - TWO*sp
          end if

          syp(i,j,kc) = sp
          sym(i,j,kc) = sm

       end do
    end do
#endif

#if (BL_SPACEDIM == 3)
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! z-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    ! compute s at z-edges

    ! compute van Leer slopes in z-direction

    do j=ilo2-1,ihi2+1
       do i=ilo1-1,ihi1+1

          ! compute on slab below
          k = k3d-1
          dsc = HALF * (s(i,j,k+1,n) - s(i,j,k-1,n))
          dsl = TWO  * (s(i,j,k  ,n) - s(i,j,k-1,n))
          dsr = TWO  * (s(i,j,k+1,n) - s(i,j,k  ,n))
          if (dsl*dsr .gt. ZERO) then
             dsvlm = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
          else
             dsvlm = ZERO
          end if

          ! compute on slab above
          k = k3d+1
          dsc = HALF * (s(i,j,k+1,n) - s(i,j,k-1,n))
          dsl = TWO  * (s(i,j,k  ,n) - s(i,j,k-1,n))
          dsr = TWO  * (s(i,j,k+1,n) - s(i,j,k  ,n))
          if (dsl*dsr .gt. ZERO) then
             dsvlp = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
          else
             dsvlp = ZERO
          end if

          ! compute on current slab
          k = k3d
          dsc = HALF * (s(i,j,k+1,n) - s(i,j,k-1,n))
          dsl = TWO  * (s(i,j,k  ,n) - s(i,j,k-1,n))
          dsr = TWO  * (s(i,j,k+1,n) - s(i,j,k  ,n))
          if (dsl*dsr .gt. ZERO) then
             dsvl0 = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
          else
             dsvl0 = ZERO
          end if

          ! interpolate to lo face
          k = k3d
          sm = HALF*(s(i,j,k,n)+s(i,j,k-1,n)) - SIXTH*(dsvl0-dsvlm)
          ! make sure sedge lies in between adjacent cell-centered values
          sm = max(sm,min(s(i,j,k,n),s(i,j,k-1,n)))
          sm = min(sm,max(s(i,j,k,n),s(i,j,k-1,n)))

          ! interpolate to hi face
          k = k3d+1
          sp = HALF*(s(i,j,k,n)+s(i,j,k-1,n)) - SIXTH*(dsvlp-dsvl0)

          ! make sure sedge lies in between adjacent cell-centered values
          sp = max(sp,min(s(i,j,k,n),s(i,j,k-1,n)))
          sp = min(sp,max(s(i,j,k,n),s(i,j,k-1,n)))

          ! flatten the parabola BEFORE doing the other
          ! monotonization -- this is the method that Flash does
          sm = flatn(i,j,k3d)*sm + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)
          sp = flatn(i,j,k3d)*sp + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)

          ! modify using quadratic limiters
          if ((sp-s(i,j,k3d,n))*(s(i,j,k3d,n)-sm) .le. ZERO) then
             sp = s(i,j,k3d,n)
             sm = s(i,j,k3d,n)

          else if (abs(sp-s(i,j,k3d,n)) .ge. TWO*abs(sm-s(i,j,k3d,n))) then
          !else if (-(sp-sm)**2/SIX > &
          !     (sp - sm)*(s(i,j,k3d) - HALF*(sm + sp))) then
             sp = THREE*s(i,j,k3d,n) - TWO*sm

          else if (abs(sm-s(i,j,k3d,n)) .ge. TWO*abs(sp-s(i,j,k3d,n))) then
          !else if ((sp-sm)*(s(i,j,k3d) - HALF*(sm + sp)) > &
          !     (sp - sm)**2/SIX) then
             sm = THREE*s(i,j,k3d,n) - TWO*sp
          end if

          szp(i,j,kc) = sp
          szm(i,j,kc) = sm

       end do
    end do
#endif

    call bl_deallocate(dsvl)
    call bl_deallocate(sedge)

  end subroutine ppm_type1

  ! :::
  ! ::: ----------------------------------------------------------------
  ! :::

  subroutine ppm_type2(s, s_lo, s_hi, ncomp, n, &
                       flatn, f_lo, f_hi, &
                       sxm, sxp, &
#if BL_SPACEDIM >= 2
                       sym, syp, &
#endif
#if BL_SPACEDIM == 3
                       szm, szp, &
#endif
                       sd_lo, sd_hi, &
                       ilo1, ilo2, ihi1, ihi2, dx, k3d, kc)

    use amrex_mempool_module, only : bl_allocate, bl_deallocate
    use meth_params_module, only : ppm_type

    use amrex_error_module
    use amrex_fort_module, only : rt => amrex_real
    implicit none

    integer, intent(in) ::  s_lo(3),  s_hi(3)
    integer, intent(in) :: ncomp, n
    integer, intent(in) :: sd_lo(3), sd_hi(3)
    integer, intent(in) ::  f_lo(3),  f_hi(3)
    integer, intent(in) :: ilo1, ilo2, ihi1, ihi2
    integer, intent(in) :: k3d, kc

    real(rt), intent(in) ::     s( s_lo(1): s_hi(1), s_lo(2): s_hi(2), s_lo(3): s_hi(3), ncomp)
    real(rt), intent(in) :: flatn(f_lo(1):f_hi(1),f_lo(2):f_hi(2),f_lo(3):f_hi(3))
    real(rt), intent(inout) :: sxm( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(inout) :: sxp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#if BL_SPACEDIM >= 2
    real(rt), intent(inout) :: sym( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(inout) :: syp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#endif
#if BL_SPACEDIM == 3
    real(rt), intent(inout) :: szm( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(inout) :: szp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#endif
    real(rt), intent(in) :: dx(3)


    ! local
    integer i,j,k
    logical extremum, bigp, bigm

    real(rt)         D2, D2C, D2L, D2R, D2LIM, alphap, alpham
    real(rt)         sgn
    real(rt)         dafacem, dafacep, dabarm, dabarp, dafacemin, dabarmin
    real(rt)         dachkm, dachkp
    real(rt)         amax, delam, delap

    ! s_{\ib,+}, s_{\ib,-}
    real(rt)         :: sm, sp

    ! s_{i+\half}^{H.O.}
    real(rt)        , pointer :: sedge(:,:)
    real(rt)        , pointer :: sedgez(:,:,:)

    ! constant used in Colella 2008
    real(rt)        , parameter :: C = 1.25e0_rt

    ! a constant used for testing extrema
    real(rt), parameter :: SMALL = 1.e-10_rt

#ifndef AMREX_USE_CUDA    
    if (ppm_type .ne. 2) &
         call amrex_error("Should have ppm_type = 2 in ppm_type2")

    if (s_lo(1) .gt. ilo1-3 .or. s_hi(1) .lt. ihi1+3) then 
         call amrex_error("Need more ghost cells on array in ppm_type1")
    end if

#if (BL_SPACEDIM >= 2)
    if (s_lo(2) .gt. ilo2-3 .or. s_hi(2) .lt. ihi2+3) then
         call amrex_error("Need more ghost cells on array in ppm_type1")
    end if
#endif
#endif

    ! edge-centered indexing
    call bl_allocate(sedge, ilo1-2, ihi1+3, ilo2-2*dg(2), ihi2+3*dg(2))
    call bl_allocate(sedgez,ilo1-1, ihi1+1, ilo2-dg(2), ihi2+dg(2), k3d-dg(2),k3d+2*dg(2))

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! x-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    ! compute s at x-edges

    ! interpolate s to x-edges
    do j=ilo2-dg(2), ihi2+dg(2)
       do i=ilo1-2, ihi1+3
          sedge(i,j) = SEVEN12TH*(s(i-1,j,k3d,n)+s(i  ,j,k3d,n)) &
               - TWELFTH*(s(i-2,j,k3d,n)+s(i+1,j,k3d,n))
          !
          ! limit sedge
          !
          if ((sedge(i,j)-s(i-1,j,k3d,n))*(s(i,j,k3d,n)-sedge(i,j)) .lt. ZERO) then
             D2  = THREE*(s(i-1,j,k3d,n)-TWO*sedge(i,j)+s(i,j,k3d,n))
             D2L = s(i-2,j,k3d,n)-TWO*s(i-1,j,k3d,n)+s(i,j,k3d,n)
             D2R = s(i-1,j,k3d,n)-TWO*s(i,j,k3d,n)+s(i+1,j,k3d,n)
             sgn = sign(ONE,D2)
             D2LIM = sgn*max(min(C*sgn*D2L,C*sgn*D2R,sgn*D2),ZERO)
             sedge(i,j) = HALF*(s(i-1,j,k3d,n)+s(i,j,k3d,n)) - SIXTH*D2LIM
          end if
       end do
    end do
    !
    ! Use Colella 2008 limiters.
    !
    ! This is a new version of the algorithm to eliminate sensitivity to roundoff.
    !
    do j=ilo2-dg(2), ihi2+dg(2)
       do i=ilo1-1,ihi1+1

          alphap   = sedge(i+1,j)-s(i,j,k3d,n)
          alpham   = sedge(i  ,j)-s(i,j,k3d,n)
          bigp     = abs(alphap).gt.TWO*abs(alpham)
          bigm     = abs(alpham).gt.TWO*abs(alphap)
          extremum = .false.

          if (alpham*alphap .ge. ZERO) then
             extremum = .true.
          else if (bigp .or. bigm) then
             !
             ! Possible extremum. We look at cell centered values and face
             ! centered values for a change in sign in the differences adjacent to
             ! the cell. We use the pair of differences whose minimum magnitude is the
             ! largest, and thus least susceptible to sensitivity to roundoff.
             !
             dafacem   = sedge(i,j) - sedge(i-1,j)
             dafacep   = sedge(i+2,j) - sedge(i+1,j)
             dabarm    = s(i,j,k3d,n) - s(i-1,j,k3d,n)
             dabarp    = s(i+1,j,k3d,n) - s(i,j,k3d,n)
             dafacemin = min(abs(dafacem),abs(dafacep))
             dabarmin  = min(abs(dabarm),abs(dabarp))
             if (dafacemin.ge.dabarmin) then
                dachkm = dafacem
                dachkp = dafacep
             else
                dachkm = dabarm
                dachkp = dabarp
             endif
             extremum = (dachkm*dachkp .le. ZERO)
          end if

          if (extremum) then
             D2     = SIX*(alpham + alphap)
             D2L    = s(i-2,j,k3d,n)-TWO*s(i-1,j,k3d,n)+s(i,j,k3d,n)
             D2R    = s(i,j,k3d,n)-TWO*s(i+1,j,k3d,n)+s(i+2,j,k3d,n)
             D2C    = s(i-1,j,k3d,n)-TWO*s(i,j,k3d,n)+s(i+1,j,k3d,n)
             sgn    = sign(ONE,D2)
             D2LIM  = max(min(sgn*D2,C*sgn*D2L,C*sgn*D2R,C*sgn*D2C),ZERO)
             alpham = alpham*D2LIM/max(abs(D2), SMALL)
             alphap = alphap*D2LIM/max(abs(D2), SMALL)
          else
             if (bigp) then
                sgn   = sign(ONE,alpham)
                amax  = -alphap**2 / (4*(alpham + alphap))
                delam = s(i-1,j,k3d,n) - s(i,j,k3d,n)
                if (sgn*amax .ge. sgn*delam) then
                   if (sgn*(delam - alpham).ge. SMALL) then
                      alphap = (-TWO*delam - TWO*sgn*sqrt(delam**2 - delam*alpham))
                   else
                      alphap = -TWO*alpham
                   endif
                endif
             end if
             if (bigm) then
                sgn   = sign(ONE,alphap)
                amax  = -alpham**2 / (4*(alpham + alphap))
                delap = s(i+1,j,k3d,n) - s(i,j,k3d,n)
                if (sgn*amax .ge. sgn*delap) then
                   if (sgn*(delap - alphap).ge. SMALL) then
                      alpham = (-TWO*delap - TWO*sgn*sqrt(delap**2 - delap*alphap))
                   else
                      alpham = -TWO*alphap
                   endif
                endif
             end if
          end if

          sm = s(i,j,k3d,n) + alpham
          sp = s(i,j,k3d,n) + alphap

          ! flatten the parabola AFTER doing the monotonization
          sm = flatn(i,j,k3d)*sm + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)
          sp = flatn(i,j,k3d)*sp + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)

          sxp(i,j,kc) = sp
          sxm(i,j,kc) = sm

       end do
    end do


#if (BL_SPACEDIM >= 2)
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! y-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    ! compute s at y-edges

    ! interpolate s to y-edges
    do j=ilo2-2, ihi2+3
       do i=ilo1-1, ihi1+1
          sedge(i,j) = SEVEN12TH*(s(i,j-1,k3d,n)+s(i,j,k3d,n)) &
               - TWELFTH*(s(i,j-2,k3d,n)+s(i,j+1,k3d,n))
          !
          ! limit sedge
          !
          if ((sedge(i,j)-s(i,j-1,k3d,n))*(s(i,j,k3d,n)-sedge(i,j)) .lt. ZERO) then
             D2  = THREE*(s(i,j-1,k3d,n)-TWO*sedge(i,j)+s(i,j,k3d,n))
             D2L = s(i,j-2,k3d,n)-TWO*s(i,j-1,k3d,n)+s(i,j,k3d,n)
             D2R = s(i,j-1,k3d,n)-TWO*s(i,j,k3d,n)+s(i,j+1,k3d,n)
             sgn = sign(ONE,D2)
             D2LIM = sgn*max(min(C*sgn*D2L,C*sgn*D2R,sgn*D2),ZERO)
             sedge(i,j) = HALF*(s(i,j-1,k3d,n)+s(i,j,k3d,n)) - SIXTH*D2LIM
          end if
       end do
    end do
    !
    ! Use Colella 2008 limiters.
    !
    ! This is a new version of the algorithm to eliminate sensitivity to roundoff.
    !
    do j=ilo2-1, ihi2+1
       do i=ilo1-1, ihi1+1

          alphap   = sedge(i,j+1)-s(i,j,k3d,n)
          alpham   = sedge(i,j  )-s(i,j,k3d,n)
          bigp     = abs(alphap).gt.TWO*abs(alpham)
          bigm     = abs(alpham).gt.TWO*abs(alphap)
          extremum = .false.

          if (alpham*alphap .ge. ZERO) then
             extremum = .true.
          else if (bigp .or. bigm) then
             !
             ! Possible extremum. We look at cell centered values and face
             ! centered values for a change in sign in the differences adjacent to
             ! the cell. We use the pair of differences whose minimum magnitude is the
             ! largest, and thus least susceptible to sensitivity to roundoff.
             !
             dafacem   = sedge(i,j) - sedge(i,j-1)
             dafacep   = sedge(i,j+2) - sedge(i,j+1)
             dabarm    = s(i,j,k3d,n) - s(i,j-1,k3d,n)
             dabarp    = s(i,j+1,k3d,n) - s(i,j,k3d,n)
             dafacemin = min(abs(dafacem),abs(dafacep))
             dabarmin  = min(abs(dabarm),abs(dabarp))
             if (dafacemin.ge.dabarmin) then
                dachkm = dafacem
                dachkp = dafacep
             else
                dachkm = dabarm
                dachkp = dabarp
             endif
             extremum = (dachkm*dachkp .le. ZERO)
          end if

          if (extremum) then
             D2     = SIX*(alpham + alphap)
             D2L    = s(i,j-2,k3d,n)-TWO*s(i,j-1,k3d,n)+s(i,j,k3d,n)
             D2R    = s(i,j,k3d,n)-TWO*s(i,j+1,k3d,n)+s(i,j+2,k3d,n)
             D2C    = s(i,j-1,k3d,n)-TWO*s(i,j,k3d,n)+s(i,j+1,k3d,n)
             sgn    = sign(ONE,D2)
             D2LIM  = max(min(sgn*D2,C*sgn*D2L,C*sgn*D2R,C*sgn*D2C),ZERO)
             alpham = alpham*D2LIM/max(abs(D2), SMALL)
             alphap = alphap*D2LIM/max(abs(D2), SMALL)
          else
             if (bigp) then
                sgn   = sign(ONE,alpham)
                amax  = -alphap**2 / (4*(alpham + alphap))
                delam = s(i,j-1,k3d,n) - s(i,j,k3d,n)
                if (sgn*amax .ge. sgn*delam) then
                   if (sgn*(delam - alpham).ge. SMALL) then
                      alphap = (-TWO*delam - TWO*sgn*sqrt(delam**2 - delam*alpham))
                   else
                      alphap = -TWO*alpham
                   endif
                endif
             end if
             if (bigm) then
                sgn   = sign(ONE,alphap)
                amax  = -alpham**2 / (4*(alpham + alphap))
                delap = s(i,j+1,k3d,n) - s(i,j,k3d,n)
                if (sgn*amax .ge. sgn*delap) then
                   if (sgn*(delap - alphap).ge. SMALL) then
                      alpham = (-TWO*delap - TWO*sgn*sqrt(delap**2 - delap*alphap))
                   else
                      alpham = -TWO*alphap
                   endif
                endif
             end if
          end if

          sm = s(i,j,k3d,n) + alpham
          sp = s(i,j,k3d,n) + alphap

          ! flatten the parabola AFTER doing the monotonization
          sm = flatn(i,j,k3d)*sm + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)
          sp = flatn(i,j,k3d)*sp + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)

          syp(i,j,kc) = sp
          sym(i,j,kc) = sm

       end do
    end do
#endif

#if (BL_SPACEDIM == 3)
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! z-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    ! compute s at z-edges

    ! interpolate s to z-edges
    do k=k3d-1, k3d+2
       do j=ilo2-1, ihi2+1
          !dir$ ivdep
          do i=ilo1-1, ihi1+1
             sedgez(i,j,k) = SEVEN12TH*(s(i,j,k-1,n)+s(i,j,k,n)) &
                  - TWELFTH*(s(i,j,k-2,n)+s(i,j,k+1,n))
             !
             ! limit sedgez
             !
             if ((sedgez(i,j,k)-s(i,j,k-1,n))*(s(i,j,k,n)-sedgez(i,j,k)) .lt. ZERO) then
                D2  = THREE*(s(i,j,k-1,n)-TWO*sedgez(i,j,k)+s(i,j,k,n))
                D2L = s(i,j,k-2,n)-TWO*s(i,j,k-1,n)+s(i,j,k,n)
                D2R = s(i,j,k-1,n)-TWO*s(i,j,k,n)+s(i,j,k+1,n)
                sgn = sign(ONE,D2)
                D2LIM = sgn*max(min(C*sgn*D2L,C*sgn*D2R,sgn*D2),ZERO)
                sedgez(i,j,k) = HALF*(s(i,j,k-1,n)+s(i,j,k,n)) - SIXTH*D2LIM
             end if
          end do
       end do
    end do
    !
    ! Use Colella 2008 limiters.
    !
    ! This is a new version of the algorithm to eliminate sensitivity to roundoff.
    !
    k = k3d
    do j=ilo2-1, ihi2+1
       do i=ilo1-1, ihi1+1

          alphap   = sedgez(i,j,k+1)-s(i,j,k,n)
          alpham   = sedgez(i,j,k  )-s(i,j,k,n)
          bigp     = abs(alphap).gt.TWO*abs(alpham)
          bigm     = abs(alpham).gt.TWO*abs(alphap)
          extremum = .false.

          if (alpham*alphap .ge. ZERO) then
             extremum = .true.
          else if (bigp .or. bigm) then
             !
             ! Possible extremum. We look at cell centered values and face
             ! centered values for a change in sign in the differences adjacent to
             ! the cell. We use the pair of differences whose minimum magnitude is the
             ! largest, and thus least susceptible to sensitivity to roundoff.
             !
             dafacem   = sedgez(i,j,k) - sedgez(i,j,k-1)
             dafacep   = sedgez(i,j,k+2) - sedgez(i,j,k+1)
             dabarm    = s(i,j,k,n) - s(i,j,k-1,n)
             dabarp    = s(i,j,k+1,n) - s(i,j,k,n)
             dafacemin = min(abs(dafacem),abs(dafacep))
             dabarmin  = min(abs(dabarm),abs(dabarp))
             if (dafacemin.ge.dabarmin) then
                dachkm = dafacem
                dachkp = dafacep
             else
                dachkm = dabarm
                dachkp = dabarp
             endif
             extremum = (dachkm*dachkp .le. ZERO)
          end if

          if (extremum) then
             D2     = SIX*(alpham + alphap)
             D2L    = s(i,j,k-2,n)-TWO*s(i,j,k-1,n)+s(i,j,k,n)
             D2R    = s(i,j,k,n)-TWO*s(i,j,k+1,n)+s(i,j,k+2,n)
             D2C    = s(i,j,k-1,n)-TWO*s(i,j,k,n)+s(i,j,k+1,n)
             sgn    = sign(ONE,D2)
             D2LIM  = max(min(sgn*D2,C*sgn*D2L,C*sgn*D2R,C*sgn*D2C),ZERO)
             alpham = alpham*D2LIM/max(abs(D2), SMALL)
             alphap = alphap*D2LIM/max(abs(D2), SMALL)
          else
             if (bigp) then
                sgn   = sign(ONE,alpham)
                amax  = -alphap**2 / (4*(alpham + alphap))
                delam = s(i,j,k-1,n) - s(i,j,k,n)
                if (sgn*amax .ge. sgn*delam) then
                   if (sgn*(delam - alpham).ge. SMALL) then
                      alphap = (-TWO*delam - TWO*sgn*sqrt(delam**2 - delam*alpham))
                   else
                      alphap = -TWO*alpham
                   endif
                endif
             end if
             if (bigm) then
                sgn   = sign(ONE,alphap)
                amax  = -alpham**2 / (4*(alpham + alphap))
                delap = s(i,j,k+1,n) - s(i,j,k,n)
                if (sgn*amax .ge. sgn*delap) then
                   if (sgn*(delap - alphap).ge. SMALL) then
                      alpham = (-TWO*delap - TWO*sgn*sqrt(delap**2 - delap*alphap))
                   else
                      alpham = -TWO*alphap
                   endif
                endif
             end if
          end if

          sm = s(i,j,k,n) + alpham
          sp = s(i,j,k,n) + alphap

          ! flatten the parabola AFTER doing the monotonization (note k = k3d here)
          sm = flatn(i,j,k3d)*sm + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)
          sp = flatn(i,j,k3d)*sp + (ONE-flatn(i,j,k3d))*s(i,j,k3d,n)

          szp(i,j,kc) = sp
          szm(i,j,kc) = sm

       end do
    end do
#endif

    call bl_deallocate(sedge)
    call bl_deallocate(sedgez)

  end subroutine ppm_type2


  subroutine ppm_int_profile(s, s_lo, s_hi, ncomp, n, &
                             q, qd_lo, qd_hi, &
                             qaux, qa_lo, qa_hi, &
                             sxm, sxp, &
#if BL_SPACEDIM >= 2
                             sym, syp, &
#endif
#if BL_SPACEDIM == 3
                             szm, szp, &
#endif
                             sd_lo, sd_hi, &
                             Ip, Im, I_lo, I_hi, icomp, ic, &
                             ilo1, ilo2, ihi1, ihi2, dx, dt, k3d, kc)

    use meth_params_module, only : NQAUX, QC, NQ, QU, QV, QW

    implicit none

    integer, intent(in) ::  s_lo(3),  s_hi(3)
    integer, intent(in) :: ncomp, n
    integer, intent(in) :: icomp, ic
    integer, intent(in) :: qd_lo(3), qd_hi(3)
    integer, intent(in) :: qa_lo(3), qa_hi(3)
    integer, intent(in) :: sd_lo(3), sd_hi(3)
    integer, intent(in) ::  I_lo(3),  I_hi(3)
    integer, intent(in) :: ilo1, ilo2, ihi1, ihi2
    integer, intent(in) :: k3d, kc

    real(rt), intent(in) ::     s( s_lo(1): s_hi(1), s_lo(2): s_hi(2), s_lo(3): s_hi(3), ncomp)
    real(rt), intent(in) ::     q(qd_lo(1):qd_hi(1),qd_lo(2):qd_hi(2),qd_lo(3):qd_hi(3), NQ)
    real(rt), intent(in) ::  qaux(qa_lo(1):qa_hi(1),qa_lo(2):qa_hi(2),qa_lo(3):qa_hi(3), NQAUX)
    real(rt), intent(in) ::   sxm( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(in) ::   sxp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#if BL_SPACEDIM >= 2
    real(rt), intent(in) ::   sym( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(in) ::   syp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#endif
#if BL_SPACEDIM == 3
    real(rt), intent(in) ::   szm( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
    real(rt), intent(in) ::   szp( sd_lo(1): sd_hi(1), sd_lo(2): sd_hi(2), sd_lo(3): sd_hi(3))
#endif
    real(rt), intent(inout) :: Ip(I_lo(1):I_hi(1),I_lo(2):I_hi(2),I_lo(3):I_hi(3),1:BL_SPACEDIM,1:3, icomp)
    real(rt), intent(inout) :: Im(I_lo(1):I_hi(1),I_lo(2):I_hi(2),I_lo(3):I_hi(3),1:BL_SPACEDIM,1:3, icomp)

    real(rt), intent(in) :: dx(3), dt


    ! local
    integer i,j,k

    real(rt)         dtdx, dtdy, dtdz
    real(rt)         sigma, s6
    real(rt)         :: sm, sp

    dtdx = dt/dx(1)
#if (BL_SPACEDIM >= 2)
    dtdy = dt/dx(2)
#endif
#if (BL_SPACEDIM == 3)
    dtdz = dt/dx(3)
#endif

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! x-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    do j = ilo2-dg(2), ihi2+dg(2)
       do i = ilo1-1, ihi1+1

          ! copy sedge into sp and sm
          sp = sxp(i,j,kc)
          sm = sxm(i,j,kc)


          ! compute x-component of Ip and Im
          s6 = SIX*s(i,j,k3d,n) - THREE*(sm+sp)

          ! Ip/m is the integral under the parabola for the extent
          ! that a wave can travel over a timestep
          !
          ! Ip integrates to the right edge of a cell
          ! Im integrates to the left edge of a cell

          ! u-c wave
          sigma = abs(q(i,j,k3d,QU)-qaux(i,j,k3d,QC))*dtdx

          if (q(i,j,k3d,QU)-qaux(i,j,k3d,QC) <= ZERO) then
             Ip(i,j,kc,1,1,ic) = sp
          else
             Ip(i,j,kc,1,1,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QU)-qaux(i,j,k3d,QC) >= ZERO) then
             Im(i,j,kc,1,1,ic) = sm
          else
             Im(i,j,kc,1,1,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

          ! u wave
          sigma = abs(q(i,j,k3d,QU))*dtdx

          if (q(i,j,k3d,QU) <= ZERO) then
             Ip(i,j,kc,1,2,ic) = sp
          else
             Ip(i,j,kc,1,2,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QU) >= ZERO) then
             Im(i,j,kc,1,2,ic) = sm
          else
             Im(i,j,kc,1,2,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

          ! u+c wave
          sigma = abs(q(i,j,k3d,QU)+qaux(i,j,k3d,QC))*dtdx

          if (q(i,j,k3d,QU)+qaux(i,j,k3d,QC) <= ZERO) then
             Ip(i,j,kc,1,3,ic) = sp
          else
             Ip(i,j,kc,1,3,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QU)+qaux(i,j,k3d,QC) >= ZERO) then
             Im(i,j,kc,1,3,ic) = sm
          else
             Im(i,j,kc,1,3,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

       end do
    end do

#if (BL_SPACEDIM >= 2)
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! y-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    do j = ilo2-dg(2), ihi2+dg(2)
       do i = ilo1-1, ihi1+1

          ! copy sedge into sp and sm
          sp = syp(i,j,kc)
          sm = sym(i,j,kc)

          ! compute y-component of Ip and Im
          s6 = SIX*s(i,j,k3d,n) - THREE*(sm+sp)

          ! v-c wave
          sigma = abs(q(i,j,k3d,QV)-qaux(i,j,k3d,QC))*dtdy

          if (q(i,j,k3d,QV)-qaux(i,j,k3d,QC) <= ZERO) then
             Ip(i,j,kc,2,1,ic) = sp
          else
             Ip(i,j,kc,2,1,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QV)-qaux(i,j,k3d,QC) >= ZERO) then
             Im(i,j,kc,2,1,ic) = sm
          else
             Im(i,j,kc,2,1,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

          ! v wave
          sigma = abs(q(i,j,k3d,QV))*dtdy

          if (q(i,j,k3d,QV) <= ZERO) then
             Ip(i,j,kc,2,2,ic) = sp
          else
             Ip(i,j,kc,2,2,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QV) >= ZERO) then
             Im(i,j,kc,2,2,ic) = sm
          else
             Im(i,j,kc,2,2,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

          ! v+c wave
          sigma = abs(q(i,j,k3d,QV)+qaux(i,j,k3d,QC))*dtdy

          if (q(i,j,k3d,QV)+qaux(i,j,k3d,QC) <= ZERO) then
             Ip(i,j,kc,2,3,ic) = sp
          else
             Ip(i,j,kc,2,3,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QV)+qaux(i,j,k3d,QC) >= ZERO) then
             Im(i,j,kc,2,3,ic) = sm
          else
             Im(i,j,kc,2,3,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

       end do
    end do
#endif

#if (BL_SPACEDIM == 3)
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! z-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    do j=ilo2-1,ihi2+1
       do i=ilo1-1,ihi1+1

          sp = szp(i,j,kc)
          sm = szm(i,j,kc)

          ! compute z-component of Ip and Im
          s6 = SIX*s(i,j,k3d,n) - THREE*(sm+sp)

          ! w-c wave
          sigma = abs(q(i,j,k3d,QW)-qaux(i,j,k3d,QC))*dtdz

          if (q(i,j,k3d,QW)-qaux(i,j,k3d,QC) <= ZERO) then
             Ip(i,j,kc,3,1,ic) = sp
          else
             Ip(i,j,kc,3,1,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QW)-qaux(i,j,k3d,QC) >= ZERO) then
             Im(i,j,kc,3,1,ic) = sm
          else
             Im(i,j,kc,3,1,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

          ! w wave
          sigma = abs(q(i,j,k3d,QW))*dtdz

          if (q(i,j,k3d,QW) <= ZERO) then
             Ip(i,j,kc,3,2,ic) = sp
          else
             Ip(i,j,kc,3,2,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QW) >= ZERO) then
             Im(i,j,kc,3,2,ic) = sm
          else
             Im(i,j,kc,3,2,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

          ! w+c wave
          sigma = abs(q(i,j,k3d,QW)+qaux(i,j,k3d,QC))*dtdz

          if (q(i,j,k3d,QW)+qaux(i,j,k3d,QC) <= ZERO) then
             Ip(i,j,kc,3,3,ic) = sp
          else
             Ip(i,j,kc,3,3,ic) = sp - &
               HALF*sigma*(sp-sm-(ONE-TWO3RD*sigma)*s6)
          endif

          if (q(i,j,k3d,QW)+qaux(i,j,k3d,QC) >= ZERO) then
             Im(i,j,kc,3,3,ic) = sm
          else
             Im(i,j,kc,3,3,ic) = sm + &
               HALF*sigma*(sp-sm+(ONE-TWO3RD*sigma)*s6)
          endif

       end do
    end do
#endif

  end subroutine ppm_int_profile


  
  subroutine ca_ppm_reconstruct_cuda(lo, hi, &
                                     s, s_lo, s_hi, &
                                     flatn, f_lo, f_hi, &
                                     qm, qm_lo, qm_hi, &
                                     qp, qp_lo, qp_hi) bind(c,name='ca_ppm_reconstruct_cuda')

    use meth_params_module, only: NQ

    implicit none

    integer,  intent(in   ) :: lo(3), hi(3)
    integer,  intent(in   ) :: s_lo(3), s_hi(3)
    integer,  intent(in   ) :: f_lo(3), f_hi(3)
    integer,  intent(in   ) :: qm_lo(3), qm_hi(3)
    integer,  intent(in   ) :: qp_lo(3), qp_hi(3)

    real(rt), intent(in   ) :: s(s_lo(1):s_hi(1), s_lo(2):s_hi(2), s_lo(3):s_hi(3), NQ)
    real(rt), intent(in   ) :: flatn(f_lo(1):f_hi(1), f_lo(2):f_hi(2), f_lo(3):f_hi(3))
    real(rt), intent(inout) :: qm(qm_lo(1):qm_hi(1),qm_lo(2):qm_hi(2),qm_lo(3):qm_hi(3),NQ,3)
    real(rt), intent(inout) :: qp(qp_lo(1):qp_hi(1),qp_lo(2):qp_hi(2),qp_lo(3):qp_hi(3),NQ,3)

    ! local
    integer :: i, j, k, n

    real(rt) :: dsl, dsr, dsc
    real(rt) :: dsvl_l, dsvl_r
    real(rt) :: sigma, s6

    ! s_{\ib,+}, s_{\ib,-}
    real(rt) :: sm, sp

    !$gpu

#ifndef AMREX_USE_GPU
    if (s_lo(1) .gt. lo(1)-3 .or. s_lo(2) .gt. lo(2)-3 .or. s_lo(3) .gt. lo(3)-3) then
         print *,'Low bounds of array: ',s_lo(1), s_lo(2),s_lo(3)
         print *,'Low bounds of  loop: ',lo(1),lo(2),lo(3)
         call amrex_error("Need more ghost cells on array in ppm_type1")
    end if

    if (s_hi(1) .lt. hi(1)+3 .or. s_hi(2) .lt. hi(2)+3 .or. s_hi(3) .lt. hi(3)+3) then
         print *,'Hi  bounds of array: ',s_hi(1), s_hi(2), s_hi(3)
         print *,'Hi  bounds of  loop: ',hi(1),hi(2),hi(3)
         call amrex_error("Need more ghost cells on array in ppm_type1")
      end if
#endif

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! x-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    do n = 1, NQ
       do k = lo(3), hi(3)
          do j = lo(2), hi(2)
             do i = lo(1), hi(1)

                ! Compute van Leer slopes

                dsc = HALF * (s(i  ,j,k,n) - s(i-2,j,k,n))
                dsl = TWO  * (s(i-1,j,k,n) - s(i-2,j,k,n))
                dsr = TWO  * (s(i  ,j,k,n) - s(i-1,j,k,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_l = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_l = ZERO
                end if

                dsc = HALF * (s(i+1,j,k,n) - s(i-1,j,k,n))
                dsl = TWO  * (s(i  ,j,k,n) - s(i-1,j,k,n))
                dsr = TWO  * (s(i+1,j,k,n) - s(i  ,j,k,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_r = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_r = ZERO
                end if

                ! Interpolate s to x-edges

                sm = HALF*(s(i,j,k,n)+s(i-1,j,k,n)) - SIXTH*(dsvl_r - dsvl_l)

                ! Make sure sedge lies in between adjacent cell-centered values

                sm = max(sm, min(s(i,j,k,n),s(i-1,j,k,n)))
                sm = min(sm, max(s(i,j,k,n),s(i-1,j,k,n)))

                ! Compute van Leer slopes

                dsc = HALF * (s(i+1,j,k,n) - s(i-1,j,k,n))
                dsl = TWO  * (s(i  ,j,k,n) - s(i-1,j,k,n))
                dsr = TWO  * (s(i+1,j,k,n) - s(i  ,j,k,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_l = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_l = ZERO
                end if

                dsc = HALF * (s(i+2,j,k,n) - s(i  ,j,k,n))
                dsl = TWO  * (s(i+1,j,k,n) - s(i  ,j,k,n))
                dsr = TWO  * (s(i+2,j,k,n) - s(i+1,j,k,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_r = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_r = ZERO
                end if

                ! Interpolate s to x-edges

                sp = HALF*(s(i+1,j,k,n)+s(i,j,k,n)) - SIXTH*(dsvl_r - dsvl_l)

                ! Make sure sedge lies in between adjacent cell-centered values

                sp = max(sp, min(s(i+1,j,k,n),s(i,j,k,n)))
                sp = min(sp, max(s(i+1,j,k,n),s(i,j,k,n)))

                ! Flatten the parabola
                sm = flatn(i,j,k)*sm + (ONE-flatn(i,j,k))*s(i,j,k,n)
                sp = flatn(i,j,k)*sp + (ONE-flatn(i,j,k))*s(i,j,k,n)

                ! Modify using quadratic limiters -- note this version of the limiting comes
                ! from Colella and Sekora (2008), not the original PPM paper.
                if ((sp-s(i,j,k,n))*(s(i,j,k,n)-sm) .le. ZERO) then

                   sp = s(i,j,k,n)
                   sm = s(i,j,k,n)

                else if (abs(sp-s(i,j,k,n)) .ge. TWO*abs(sm-s(i,j,k,n))) then

                   sp = THREE*s(i,j,k,n) - TWO*sm

                else if (abs(sm-s(i,j,k,n)) .ge. TWO*abs(sp-s(i,j,k,n))) then

                   sm = THREE*s(i,j,k,n) - TWO*sp

                end if

                qp(i  ,j,k,n,1) = sp
                qm(i+1,j,k,n,1) = sm

             end do
          end do
       end do
    end do

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! y-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    do n = 1, NQ
       do k = lo(3), hi(3)
          do j = lo(2), hi(2)
             do i = lo(1), hi(1)

                ! Compute van Leer slopes

                dsc = HALF * (s(i,j  ,k,n) - s(i,j-2,k,n))
                dsl = TWO  * (s(i,j-1,k,n) - s(i,j-2,k,n))
                dsr = TWO  * (s(i,j  ,k,n) - s(i,j-1,k,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_l = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_l = ZERO
                end if

                dsc = HALF * (s(i,j+1,k,n) - s(i,j-1,k,n))
                dsl = TWO  * (s(i,j  ,k,n) - s(i,j-1,k,n))
                dsr = TWO  * (s(i,j+1,k,n) - s(i,j  ,k,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_r = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_r = ZERO
                end if

                ! Interpolate s to y-edges

                sm = HALF*(s(i,j,k,n)+s(i,j-1,k,n)) - SIXTH*(dsvl_r - dsvl_l)

                ! Make sure sedge lies in between adjacent cell-centered values

                sm = max(sm, min(s(i,j,k,n),s(i,j-1,k,n)))
                sm = min(sm, max(s(i,j,k,n),s(i,j-1,k,n)))

                ! Compute van Leer slopes

                dsc = HALF * (s(i,j+1,k,n) - s(i,j-1,k,n))
                dsl = TWO  * (s(i,j  ,k,n) - s(i,j-1,k,n))
                dsr = TWO  * (s(i,j+1,k,n) - s(i,j  ,k,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_l = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_l = ZERO
                end if

                dsc = HALF * (s(i,j+2,k,n) - s(i,j  ,k,n))
                dsl = TWO  * (s(i,j+1,k,n) - s(i,j  ,k,n))
                dsr = TWO  * (s(i,j+2,k,n) - s(i,j+1,k,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_r = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_r = ZERO
                end if

                ! Interpolate s to y-edges

                sp = HALF*(s(i,j+1,k,n)+s(i,j,k,n)) - SIXTH*(dsvl_r - dsvl_l)

                ! Make sure sedge lies in between adjacent cell-centered values

                sp = max(sp, min(s(i,j+1,k,n),s(i,j,k,n)))
                sp = min(sp, max(s(i,j+1,k,n),s(i,j,k,n)))

                ! Flatten the parabola

                sm = flatn(i,j,k)*sm + (ONE-flatn(i,j,k))*s(i,j,k,n)
                sp = flatn(i,j,k)*sp + (ONE-flatn(i,j,k))*s(i,j,k,n)

                ! Modify using quadratic limiters

                if ((sp-s(i,j,k,n))*(s(i,j,k,n)-sm) .le. ZERO) then

                   sp = s(i,j,k,n)
                   sm = s(i,j,k,n)

                else if (abs(sp-s(i,j,k,n)) .ge. TWO*abs(sm-s(i,j,k,n))) then

                   sp = THREE*s(i,j,k,n) - TWO*sm

                else if (abs(sm-s(i,j,k,n)) .ge. TWO*abs(sp-s(i,j,k,n))) then

                   sm = THREE*s(i,j,k,n) - TWO*sp

                end if

                qp(i,j  ,k,n,2) = sp
                qm(i,j+1,k,n,2) = sm

             end do
          end do
       end do
    end do

    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    ! z-direction
    !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

    do n = 1, NQ
       do k = lo(3), hi(3)
          do j = lo(2), hi(2)
             do i = lo(1), hi(1)

                ! Compute van Leer slopes

                dsc = HALF * (s(i,j,k  ,n) - s(i,j,k-2,n))
                dsl = TWO  * (s(i,j,k-1,n) - s(i,j,k-2,n))
                dsr = TWO  * (s(i,j,k  ,n) - s(i,j,k-1,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_l = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_l = ZERO
                end if

                dsc = HALF * (s(i,j,k+1,n) - s(i,j,k-1,n))
                dsl = TWO  * (s(i,j,k  ,n) - s(i,j,k-1,n))
                dsr = TWO  * (s(i,j,k+1,n) - s(i,j,k  ,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_r = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_r = ZERO
                end if

                ! Interpolate s to z-edges

                sm = HALF*(s(i,j,k,n)+s(i,j,k-1,n)) - SIXTH*(dsvl_r - dsvl_l)

                ! Make sure sedge lies in between adjacent cell-centered values

                sm = max(sm, min(s(i,j,k,n),s(i,j,k-1,n)))
                sm = min(sm, max(s(i,j,k,n),s(i,j,k-1,n)))

                ! Compute van Leer slopes

                dsc = HALF * (s(i,j,k+1,n) - s(i,j,k-1,n))
                dsl = TWO  * (s(i,j,k  ,n) - s(i,j,k-1,n))
                dsr = TWO  * (s(i,j,k+1,n) - s(i,j,k  ,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_l = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_l = ZERO
                end if

                dsc = HALF * (s(i,j,k+2,n) - s(i,j,k  ,n))
                dsl = TWO  * (s(i,j,k+1,n) - s(i,j,k  ,n))
                dsr = TWO  * (s(i,j,k+2,n) - s(i,j,k+1,n))
                if (dsl*dsr .gt. ZERO) then
                   dsvl_r = sign(ONE,dsc)*min(abs(dsc),abs(dsl),abs(dsr))
                else
                   dsvl_r = ZERO
                end if

                ! Interpolate s to z-edges

                sp = HALF*(s(i,j,k+1,n)+s(i,j,k,n)) - SIXTH*(dsvl_r - dsvl_l)

                ! Make sure sedge lies in between adjacent cell-centered values

                sp = max(sp, min(s(i,j,k+1,n),s(i,j,k,n)))
                sp = min(sp, max(s(i,j,k+1,n),s(i,j,k,n)))

                ! Flatten the parabola

                sm = flatn(i,j,k)*sm + (ONE-flatn(i,j,k))*s(i,j,k,n)
                sp = flatn(i,j,k)*sp + (ONE-flatn(i,j,k))*s(i,j,k,n)

                ! Modify using quadratic limiters

                if ((sp-s(i,j,k,n))*(s(i,j,k,n)-sm) .le. ZERO) then

                   sp = s(i,j,k,n)
                   sm = s(i,j,k,n)

                else if (abs(sp-s(i,j,k,n)) .ge. TWO*abs(sm-s(i,j,k,n))) then

                   sp = THREE*s(i,j,k,n) - TWO*sm

                else if (abs(sm-s(i,j,k,n)) .ge. TWO*abs(sp-s(i,j,k,n))) then

                   sm = THREE*s(i,j,k,n) - TWO*sp

                end if

                qp(i,j,k  ,n,3) = sp
                qm(i,j,k+1,n,3) = sm

             end do
          end do
       end do
    end do

  end subroutine ca_ppm_reconstruct_cuda

end module ppm_module
